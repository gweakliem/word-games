buildscript {
  dependencies {
    classpath "org.postgresql:postgresql:42.2.5"
    // used by jooq gradle plugin to write config
    classpath "com.sun.xml.bind:jaxb-impl:2.3.0.1"
    classpath "com.sun.xml.bind:jaxb-core:2.3.0.1"
    classpath "com.sun.activation:javax.activation:1.2.0"
  }
}

plugins {
  id "org.jetbrains.kotlin.jvm" version "1.3.11"
  id 'application'
  id "org.flywaydb.flyway" version "5.2.4"
  id "nu.studer.jooq" version "3.0.2"
  id "com.github.ben-manes.versions" version "0.20.0"
}

ext {
  deps = [
      ktor      : '1.0.1',
      // also see version in buildscript
      postgresql: '42.2.5',
      jackson   : '2.9.7',
      slf4j     : '1.7.25',
      // also see versions in buildscript
      jaxb      : '2.3.0.1',
      jaxbApi   : '2.3.0',
      activation: '1.2.0'
  ]

  dbUser = 'ktor-demo-dev'
  dbPass = 'ktor-demo-dev'
  jdbcUrl = 'jdbc:postgresql://localhost:25432/ktor-demo'
}

mainClassName = 'org.mpierce.ktordemo.KtorDemoKt'

repositories {
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  compile "io.ktor:ktor-server-core:$deps.ktor"
  compile "io.ktor:ktor-server-netty:$deps.ktor"
  compile "io.ktor:ktor-jackson:$deps.ktor"

  compile "com.fasterxml.jackson.core:jackson-databind:$deps.jackson"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$deps.jackson"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:$deps.jackson"

  runtime "ch.qos.logback:logback-classic:1.2.3"
  runtime "org.slf4j:jcl-over-slf4j:$deps.slf4j"

  compile 'com.google.inject:guice:4.2.2'

  compile 'org.skife.config:config-magic:0.17'
  compile 'commons-configuration:commons-configuration:1.10'

  compile 'com.zaxxer:HikariCP:3.2.0'
  compile 'org.jooq:jooq'
  runtime "org.postgresql:postgresql:$deps.postgresql"
  jooqRuntime "org.postgresql:postgresql:$deps.postgresql"
  jooqRuntime "com.sun.xml.bind:jaxb-impl:$deps.jaxb"
  jooqRuntime "com.sun.xml.bind:jaxb-core:$deps.jaxb"
  jooqRuntime "javax.xml.bind:jaxb-api:$deps.jaxbApi"
  jooqRuntime "com.sun.activation:javax.activation:$deps.activation"
}

configurations.all {
  // don't let commons logging creep into the classpath; use jcl-over-slf4j instead
  exclude group: 'commons-logging', module: 'commons-logging'
}

flyway {
  url = jdbcUrl
  user = dbUser
  password = dbPass
}

jooq {
  version = '3.11.7'
  edition = 'OSS'

  ktorDemo(sourceSets.main) {
    jdbc {
      driver = 'org.postgresql.Driver'
      url = jdbcUrl
      user = dbUser
      password = dbPass
    }
    generator {
      target {
        packageName = 'org.mpierce.ktordemo.jooq'
      }
      database {
        inputSchema = 'public'
        excludes = 'flyway_schema_history'
      }
      generate {
        generatedAnnotation = false
      }
    }
  }
}

compileKotlin.dependsOn(generateKtorDemoJooqSchemaSource)
generateKtorDemoJooqSchemaSource.dependsOn(flywayMigrate)

compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
